plugins {
    id 'java'
    id 'org.springframework.boot' version '4.0.0'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'checkstyle'
    id 'jacoco'
}

group = 'com.back'
version = '0.0.1-SNAPSHOT'
description = 'Demo project for Spring Boot'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // 웹이랑 벨리데이션
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // jpa, redis
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    // QueryDSL
    implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
    annotationProcessor 'com.querydsl:querydsl-apt:5.0.0:jakarta'
    annotationProcessor 'jakarta.annotation:jakarta.annotation-api'
    annotationProcessor 'jakarta.persistence:jakarta.persistence-api'

    // DB 드라이버
    runtimeOnly 'org.postgresql:postgresql'  // PostgreSQL

    // security & jwt
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'io.jsonwebtoken:jjwt-api:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.6'

    // Testcontainer(Redis 테스트용)
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testImplementation 'org.testcontainers:junit-jupiter:1.21.3'

    //swagger
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:3.0.0'

    //SMTP
    implementation 'org.springframework.boot:spring-boot-starter-mail'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'

    implementation 'org.springframework.boot:spring-boot-starter-webmvc'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-validation-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-webmvc-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    runtimeOnly 'com.h2database:h2'
    developmentOnly 'org.springframework.boot:spring-boot-h2console'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // AWS S3
    implementation platform("io.awspring.cloud:spring-cloud-aws-dependencies:4.0.0-M1")
    implementation "io.awspring.cloud:spring-cloud-aws-starter-s3"

}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs.add('-parameters')
}

tasks.withType(Checkstyle) {
    reports {
        xml.required = true
        html.required = true
    }
}

// Checkstyle 설정
checkstyle {
    toolVersion = '10.12.5'
    configFile = rootProject.file('codestyle/naver-checkstyle-rules.xml')
    configProperties = [
            suppressionFile: rootProject.file('codestyle/naver-checkstyle-suppressions.xml').absolutePath
    ]
    ignoreFailures = false
}

tasks.named('test') {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacoco {
    toolVersion = "0.8.12"
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
        csv.required = false
    }

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    '**/*Dto.class',
                    '**/*Dto$*.class',
                    '**/*Request.class',
                    '**/*Request$*.class',
                    '**/*Response.class',
                    '**/*Response$*.class',
                    '**/*Req.class',
                    '**/*Req$*.class',
                    '**/*Res.class',
                    '**/*Res$*.class',
                    '**/config/**',
                    '**/configuration/**',
                    '**/exception/**',
                    '**/error/**',
                    '**/*Application.class',
                    '**/*Initializer.class',
                    '**/Q*.class',
                    '**/entity/**',
                    '**/domain/**/*Entity.class',
                    '**/controller/**',
                    '**/toss/**'
            ])
        }))
    }
}

jacocoTestCoverageVerification {
    dependsOn jacocoTestReport

    violationRules {
        rule {
            enabled = true
            element = 'CLASS'

            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.0  // 커버리지 리포트만 생성, 빌드 실패 없음
            }
        }
    }

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    '**/*Dto.class',
                    '**/*Dto$*.class',
                    '**/*Request.class',
                    '**/*Request$*.class',
                    '**/*Response.class',
                    '**/*Response$*.class',
                    '**/*Req.class',
                    '**/*Req$*.class',
                    '**/*Res.class',
                    '**/*Res$*.class',
                    '**/config/**',
                    '**/configuration/**',
                    '**/exception/**',
                    '**/error/**',
                    '**/*Application.class',
                    '**/*Initializer.class',
                    '**/Q*.class',
                    '**/entity/**',
                    '**/domain/**/*Entity.class',
                    '**/controller/**',
                    '**/toss/**'
            ])
        }))
    }
}
